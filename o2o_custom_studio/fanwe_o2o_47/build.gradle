apply plugin: 'com.android.application'

def appName = "省点云购"

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        manifestPlaceholders = [CHANNEL_VALUE: "default"]
        applicationId "com.shengdianwang.o2o.newo2o"
        minSdkVersion 14
        versionCode 2017021904
        versionName "2017021904"
        //multiDexEnabled = true //分包
    }

    signingConfigs {
        config {
            storeFile file('D:/develop/o2o/studio_projects/o2o.jks')
            storePassword "111111"
            keyAlias "shengdianwang"
            keyPassword "111111"//将这里的“**”替换成自己的信息
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.config
            minifyEnabled false //是否混淆
            zipAlignEnabled false
            shrinkResources false //去除无用资源
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            signingConfig signingConfigs.config
            minifyEnabled false //是否混淆
            zipAlignEnabled false
            shrinkResources false //去除无用资源
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        normal {}

        dev {
            //设置测试环境的host地址
            buildConfigField("String", "API_HOST", "\"120.24.186.128\"")
            //替换测试的应用名称和渠道号
            manifestPlaceholders = [app_name     : "${appName}-测试",
                                    umeng_channel: "test"]
            //设置测试的包名后缀
            //applicationIdSuffix ".dev"
        }
    }

    //针对所有的productFlavors在如下遍历
    productFlavors.each {
        flavor ->
            //测试环境不做统一处理
            if (flavor.name != "dev") {
                //替换正式的应用名称和渠道号
                flavor.manifestPlaceholders = [app_name     : "${appName}",
                                               umeng_channel: flavor.name]
                //设置正式环境的host地址
                flavor.buildConfigField("String", "API_HOST", "\"101.200.88.185\"")
                //flavor.buildConfigField("String", "API_HOST", "\"120.24.186.128\"")
            }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent + "/${variant.buildType.name}",
                    "o2o-custom-${variant.buildType.name}-${variant.productFlavors[0].name}.apk".toLowerCase())
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':library_PhotoViewmaster')
    compile project(':library_androidshapeimageview')
    compile project(':library_androidpercentsupportextend')
    compile project(':library_xml_drawable_o2o')
    compile project(':library_span')
    compile project(':library_AndroidPullToRefresh')
    compile project(':library_map_baidu')
    compile project(':library_push_umeng')
    compile project(':library_qrcode_zxing')
    compile project(':library_pay_alipay')
    compile project(':library_social_umeng')
    compile project(':library_fanwe')
    compile project(':library_pay_unionpay')
    compile 'com.android.support:design:24.0.0'
    compile 'com.android.support:appcompat-v7:24.0.0'
    provided 'org.projectlombok:lombok:1.16.6'
    //get/set自动工具
}
